buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
        jcenter()
        maven {
            name "gradle-plugins"
            url "https://plugins.gradle.org/m2/"
        }
    }

    ext.kotlin_version = '1.2.10'

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.6.0'
    }
}

apply plugin: 'pl.allegro.tech.build.axion-release'

group 'com.pij.horrocks'

scmVersion {
    tag {
        prefix = ''
    }

    versionCreator 'versionWithBranch'
    hooks {
        pre 'fileUpdate', [file: 'README.md', pattern: { previous, context -> />$previous<\// }, replacement: { current, context -> ">$current</" }]
        pre 'fileUpdate', [file: 'README.md', pattern: { previous, context -> /:$previous'/ }, replacement: { current, context -> ":$current\'" }]
        pre 'commit', { v, p -> "Automatic update for release $v" }
        post 'push'
    }
}

allprojects {
    buildscript {
        repositories {
            mavenLocal()
            mavenCentral()
            google()
            jcenter()
            maven {
                name "gradle-plugins"
                url "https://plugins.gradle.org/m2/"
            }
        }
    }


    repositories {
        mavenLocal()
        mavenCentral()
        google()
        jcenter()
        // For Ice pick 3.*
        maven { url "https://clojars.org/repo/" }
        maven { url "https://jitpack.io" }
    }

    project.version = scmVersion.version
    project.group = rootProject.group

}

// Define dependency versions in a single place
ext {
    // Sdk and tools
    // Support library and architecture components support minSdk 14 and above.
    minSdkVersion = 14
    targetSdkVersion = 26
    compileSdkVersion = 26

    // App dependencies
    supportLibraryVersion = '27.0.2'
    guavaVersion = '18.0'
    junitVersion = '4.12'
    mockitoVersion = '2.6.1'
    mockitoKotlinVersion = '1.5.0'
    powerMockito = '1.6.2'
    hamcrestVersion = '1.3'
    runnerVersion = '1.0.1'
    rulesVersion = '1.0.1'
    espressoVersion = '3.0.1'
    autoValueVersion = '1.5.1'
    rxjava2Version = '2.1.0'
    roomVersion = "1.0.0"
    daggerVersion = '2.12'
    activityStarterVersion = '1.11-beta'
    butterknifeVersion = '8.8.1'
    dexmakerVersion = '1.2'
}


task clean(type: Delete) {
    delete rootProject.buildDir
}
